import std/os/env
import std/os/path
import std/data/linearmap
import std/data/linearset
import std/jsextern
import ext/files
import ext/idl
import ext/css
import ext/elements
import ext/doc
import ext/bcd
import processor
import common
import utils
import kk-builder
import translator

extern import
  js file "main.mjs"

extern doSetup(f: () -> <io-noexn> ()): <io-noexn> ()
  js inline "globalThis.doSetup(#1)"

pub fun main()
  with doSetup
  with handler
    final ctl throw-exn(e) -> e.show.println
  //                                  main jsdebug version .koka generator
  val dir = get-args()[0].unjust.path.nobase.nobase.nobase.nobase.nobase.string
  ensure-dir-exists(dir ++ "/dom")
  val bindings = generate-bindings("dom".path) // TODO: Async/Await
  bindings.foreach fn(lib-path, library)
    println("Writing to " ++ dir ++ " " ++ lib-path)
    write-file-sync(dir ++ "/" ++ lib-path, emit-library(library))

fun generate-css-style()
  val results = css().entries.map fn((_, value))
    val entry: css-entries = value.unsafe-cast()
    match entry.get-properties()
      Nothing -> []
      Just(props) ->
        list(0, props.length - 1).flatmap-maybe fn(i)
          val mstyle = props[i].get-style-declarations()
          match mstyle
            Nothing -> Nothing
            Just(style) -> 
              val len = style.length
              if len < 0 || len > 3 then throw("Unexpected style declaration")
              elif len == 3 then Nothing
              else
                val stylestring = style[len - 1]
                if stylestring.contains("-") then throw("Unexpected style declaration")
                else Just(stylestring)
  results.concat

fun generate-element-tag-map()
  elements().entries.foldl(LinearMap([])) fn(acc, (_,value))
    val data : elements-entries = value.unsafe-cast()
    match data.elements
      Nothing -> acc
      Just(elements) -> 
        list(0, elements.length - 1).foldl(acc) fn(acc', i)
          val element = elements[i]
          val tag = element.name()
          val interf = element.elem/interface()
          match (interf, tag)
            (Just(i'), Just(t)) ->
              acc'.update(i', LinearSet([t]), fn(old, new) old.union(new))
            _ -> acc'

fun generate-bindings(dir: path)
  val cssres = generate-css-style()
  val elementres = generate-element-tag-map()
  with compat-data()
  with doc-info()
  with translator(dir, cssres, elementres)
  idl().entries.foreach fn((shortname, value)) 
    val ast: jsarray<node> = value.unsafe-as-array()
    collect(shortname, ast)
  update-interfaces()
  translate()

fun emit-library(c: kk-library): console string
  println("Emitting to library")
  ""